plugins {
    id 'java-library'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0.116'
    id 'idea'
    id "me.modmuss50.mod-publish-plugin" version "1.1.0"
}

tasks.named('wrapper', Wrapper).configure {
    distributionType = Wrapper.DistributionType.BIN
}


def mcBase = getMinecraftBaseVersion()
def snapshotEnv = providers.environmentVariable("SNAPSHOT")
def isSnapshot = snapshotEnv.isPresent() && snapshotEnv.get().toBoolean()

version = "${mcBase}.${project.mod_version}${isSnapshot ? "-SNAPSHOT" : ""}"
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

neoForge {
    version = project.neoforge_full_version

    // This line is optional. Access Transformers are automatically detected
    // accessTransformers = project.files('src/main/resources/META-INF/accesstransformer.cfg')
    runs {
        client {
            client()
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            data()
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        // applies to all the run configs above
        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    runtimeClasspath.extendsFrom localRuntime
}

dependencies {
    implementation "dev.latvian.mods:kubejs-neoforge:${kubejs_version}"
}

repositories {
    mavenLocal()

    maven {
        name = "Kubejs"
        url "https://maven.latvian.dev/releases"
        content {
            includeGroup "dev.latvian.mods"
            includeGroup "dev.latvian.apps"
        }
    }

    maven {
        url 'https://jitpack.io'
        content {
            includeGroup "com.github.rtyley"
        }
    }

    maven {
        url "https://maven.architectury.dev"
        content {
            includeGroup "dev.architectury"
        }
    }
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            neoforge_base_two      : mcBase,
            neoforge_loader_version: neoforge_loader_version,
            neoforge_full_version: neoforge_full_version,
            version                : version,
            neo_version_range: neo_version_range,
            kubejs_version       : kubejs_version,
            kubejs_version_range       : kubejs_version_range
    ]

    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml']) {
        expand replaceProperties
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.release.set(21)
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        def naniteSecret = providers.environmentVariable("NANITE_TOKEN");
        if (naniteSecret.isPresent()) {
            maven {
                url "https://maven.nanite.dev/${isSnapshot ? "snapshots" : "releases"}"
                credentials {
                    username = "nanite"
                    password = naniteSecret.get()
                }
            }
        }
    }
}

publishMods {
    dryRun = providers.environmentVariable("CURSEFORGE_TOKEN").getOrNull() == null
    changelog = file("./CHANGELOG.md").text
    version = project.version
    file = project.provider { project.tasks.jar }.flatMap { it.archiveFile }
    modLoaders.add("neoforge")
    type = STABLE
    displayName = "[${minecraft_version}] KubeUtils ${project.version}"

    curseforge {
        projectId = curseforge_id
        accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
        minecraftVersions.add("${minecraft_version}")
        javaVersions.add(JavaVersion.VERSION_17)

        requires("kubejs")
    }

    modrinth {
        projectId = modrinth_id
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        minecraftVersions.add("${minecraft_version}")
        requires("kubejs")
    }

    github {
        accessToken = providers.environmentVariable("GITHUB_TOKEN")
        repository = "Nanite/KubeUtils"
        commitish = providers.environmentVariable("GITHUB_SHA").orElse("dryRun")
        tagName = providers.environmentVariable("GITHUB_REF_NAME").orElse("dryRun")
    }
}

/**
 * Removes the 1. from Minecraft's version, then adds a 0 to the end if the
 * version does not contain three dots.
 */
def getMinecraftBaseVersion() {
    def version = project.minecraft_version
    def parts = version.split("\\.")

    // Remove the first part of the version
    parts = parts[1..-1]

    if (parts.size() < 2) {
        parts << "0"
    }

    return parts.join(".")
}
